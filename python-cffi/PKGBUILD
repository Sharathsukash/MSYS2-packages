# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=cffi
pkgbase=python-${_realname}
pkgname=("python-${_realname}")
provides=("python3-${_realname}")
conflicts=("python3-${_realname}")
replaces=("python3-${_realname}")
pkgver=1.16.0
pkgrel=1
pkgdesc="Foreign Function Interface for Python calling C code (mingw-w64)"
url='https://cffi.readthedocs.io/'
license=('MIT')
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
msys2_references=(
  'pypi: cffi'
)
depends=("libffi"
         "python-pycparser")
makedepends=("python-setuptools" "cc")
checkdepends=("python-pytest-runner")
source=("https://pypi.org/packages/source/${_realname:0:1}/${_realname}/${_realname}-${pkgver}.tar.gz"
        "mingw-non-x86.patch")
sha256sums=('bcb3ef43e58665bbda2fb198698fcae6776483e0c4a631aa5647806c25e02cc0'
            'c8a8ea2d65932ea0b62eae56dbc8afd6baacaadc7cb284ca0e4edf7ed1460c52')

prepare() {
  cd "${srcdir}"
  cp -r "${_realname}-${pkgver}" "python-build-${CARCH}"
  cd "python-build-${CARCH}"
  patch -Np1 -i "${srcdir}"/mingw-non-x86.patch
}

build() {
  cd "${srcdir}/python-build-${CARCH}"
  /usr/bin/python setup.py build
}

package() {
  cd "${srcdir}/python-build-${CARCH}"

  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  /usr/bin/python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build

  install -D -m644 LICENSE "${pkgdir}/usr"/share/licenses/python-${_realname}/LICENSE
}